buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
    }
}

plugins {
    id "com.moowork.node" version "0.11"
}

ext {
    group = "org.cmapi.primitives"

    description = "Common Map Geospatial Notation (CMGN)"

    nexusPublishUrl   = project.hasProperty('nexusPublishUrl') ? project.property('nexusPublishUrl') : ''

    di2eNexusUsername = project.hasProperty('empUsername') ? project.property('empUsername') : ''
    di2eNexusPassword = project.hasProperty('empPassword') ? project.property('empPassword') : ''
}

configure (rootProject) {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'com.moowork.node'

    repositories {
        jcenter()
    }

    def cmapiGeneratedDir   = file("$buildDir/generated/cmapi")
    def cmapiJsonSchemaFile = file("src/schema/geonotation.schema.json")

    sourceSets.main.java.srcDirs += "$cmapiGeneratedDir/dist/java"

    javadoc {
        source = sourceSets.main.allJava
        failOnError true
        exclude '**/*.md'
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier 'javadoc'
        from javadoc.destinationDir
    }

    task generateJavaBindingsCMAPI(type: NodeTask, description: "Generates the CMAPI interface classes") {
        script = file("src/generator/generate.js")
        //args = ['-debug', '/../schema/geonotation.schema.json']

        execOverrides {
            it.workingDir = cmapiGeneratedDir
        }
        doFirst {
            cmapiGeneratedDir.mkdirs()
        }
        inputs.file "src/generator/generate.js"
        outputs.dir cmapiGeneratedDir
    }
    compileJava.dependsOn generateJavaBindingsCMAPI

    node {
        version  = '4.3.1'
        download = true
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId    = project.ext.group
                artifactId = project.name
                version    = project.version

                artifact jar
                artifact javadocJar
                artifact cmapiJsonSchemaFile
            }
        }
        repositories {
            maven {
                //url "$rootProject.buildDir/_repo1"
                url = nexusPublishUrl

                credentials {
                    username project.di2eNexusUsername
                    password project.di2eNexusPassword
                }
            }
        }
        if (project.hasProperty('di2eNexusReleaseUrl')) {
            repositories {
                maven {
                    //url "$rootProject.buildDir/_repo2"
                    url = project.property('di2eNexusReleaseUrl')

                    credentials {
                        username project.di2eNexusUsername
                        password project.di2eNexusPassword
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper, description: "Generates gradlew[.bat] scripts") {
    gradleVersion = '2.12'
}

if (JavaVersion.current().isJava8Compatible()) { // disable lint for java8
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}